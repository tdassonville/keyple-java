buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.14.0"
    }
}


apply from: '../common.gradle'

subprojects {

    group 'org.eclipse.keyple'
    apply plugin: 'pmd'


    repositories {
        mavenLocal()
        mavenCentral()
        google()
        jcenter()
    }

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }


    pmd {
        ruleSets = [
                "java-basic",
                "java-braces",
        ]
    }
    
}



//create task for to agragate javadoc
task aggregatedJavadocs(type: Javadoc, description: 'Generate javadocs from all child projects as if it was a single project', group: 'Documentation') {
    println 'Create a aggregated javadoc Task to : ' + "$buildDir/docs/javadoc"
    destinationDir = file("$buildDir/docs/javadoc")
    title = "$project.name " +  project(":java:component:keyple-core").version + " version API"
    options.author true
    options.links  'http://docs.oracle.com/javase/6/docs/api/',
            "http://d.android.com/reference/",
            'http://seek-for-android.github.io/javadoc/V4.0.0/'


    options.addStringOption 'Xdoclint:none', '-quiet'

    //init source to whatever file to avoid NO-SOURCE
    source = 'README.MD'
}

//dynamically configure the source
aggregatedJavadocs.doFirst {

    //exclude the init file
    excludes += "README.MD"

    //add Android modules javadoc
    def aggregatedAndroidProjects = [
                                  project(":keyple-plugin:keyple-plugin-android-nfc"),
                                  project(":keyple-plugin:keyple-plugin-android-omapi")]

    aggregatedAndroidProjects.each { aggregatedAndroidProject ->
            source += aggregatedAndroidProject.generateDebugJavadoc.source
            classpath += aggregatedAndroidProject.generateDebugJavadoc.classpath
            excludes += aggregatedAndroidProject.generateDebugJavadoc.excludes
            includes += aggregatedAndroidProject.generateDebugJavadoc.includes

    }

}
